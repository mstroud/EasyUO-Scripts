; [YO] Mining Suite v1.3
GoSub Initialize ; Initialize Global Vars
GoSub CreateMainMenu ; Creates Main Window

; ///////////////////////////
; //  Main Execution Loop  //
; ///////////////////////////

Mainloop:
    GoSub 8x8 ; Update Menu X/Y Coords

; /////////////////////////
; //  Mining Menu Row 1  //
; /////////////////////////

    Menu Get Options
    If #MenuRes = 1
    {
        Set %UsePackHorse #True
        Set %UsePortForge #False
    }
    If #MenuRes = 2
    {
        Set %UsePackHorse #False
        Set %UsePortForge #True
    }
    
    If #menuButton = Mine
    {
        GoSub CreateStatusMenu
        GoSub MineCircle
        Set #menuButton Nothing
        GoSub CreateMainMenu
    }
    If #menuButton = Reinitialize
    {
        GoSub CreateStatusMenu
        GoSub Initialize
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = SelfIngots
    {
        GoSub CreateStatusMenu
        Set #LObjectID %ToolBag
        Event Macro 17 0
        Gosub MoveIngots #BackpackID %ToolBag
        GoSub OrganizeIngots %ToolBag
        Set #menuButton Nothing
        GoSub CreateMainMenu
    }
    If #menuButton = BankIngots
    {
        GoSub CreateStatusMenu
        Event Macro 1 0 Bank
        Wait %ShortDelay
        Set #LObjectID %BankIngotContainer
        Event Macro 17 0
        Gosub MoveIngots %ToolBag %BankIngotContainer
        GoSub OrganizeIngots %BankIngotContainer
        Set #menuButton Nothing
        GoSub CreateMainMenu
    }

; /////////////////////////
; //  Mining Menu Row 2  //
; /////////////////////////

    If #menuButton = Smelt
    {
        GoSub CreateStatusMenu
        GoSub Smelt
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = Consolidate
    {
        GoSub CreateStatusMenu
        GoSub DumpPack
        GoSub ConsolidateOre %IronCol %IronLabel
        GoSub ConsolidateOre %DullCopperCol %DullCopperLabel
        GoSub ConsolidateOre %ShadowCol %ShadowLabel
        GoSub ConsolidateOre %CopperCol %CopperLabel
        GoSub ConsolidateOre %BronzeCol %BronzeLabel
        GoSub ConsolidateOre %GoldCol %GoldLabel
        GoSub ConsolidateOre %AgapiteCol %AgapiteLabel
        GoSub ConsolidateOre %VeriteCol %VeriteLabel
        GoSub ConsolidateOre %ValoriteCol %ValoriteLabel
        If %UsePackHorse = #True
            GoSub LoadHorse
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = DumpOre
    {
        GoSub CreateStatusMenu
        GoSub DumpPack
        If %UsePackHorse = #True
            GoSub DumpHorse
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = RareOre
    {
        GoSub CreateStatusMenu
        GoSub PickupRareOre
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }

; ////////////////////////
; //  Pack Animal Menu  //
; ////////////////////////

    If #menuButton = LoadHorse
    {
        GoSub CreateStatusMenu
        GoSub LoadHorse
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = Come
    {
        Event Macro 1 0 All Come
        Set #menuButton Nothing
    }
    If #menuButton = Follow
    {
        Event Macro 1 0 All Follow Me
        Set #menuButton Nothing
    }
    If #menuButton = Stay
    {
        Event Macro 1 0 All Stay
        Set #menuButton Nothing
    }

; //////////////////////
; //  Auto-Mine Menu  //
; //////////////////////

    If #menuButton = MineMinoc
    {
        GoSub CreateStatusMenu
        GoSub MineMinoc
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = RecallMinoc
    {
        GoSub CreateStatusMenu
        GoSub Recall 4
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = MineAvarice1
    {
        GoSub CreateStatusMenu
        GoSub MineAvarice1
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = RecallAvarice1
    {
        GoSub CreateStatusMenu
        GoSub Recall 5
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = MineAvarice2
    {
        GoSub CreateStatusMenu
        GoSub MineAvarice2
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = RecallAvarice2
    {
        GoSub CreateStatusMenu
        GoSub Recall 6
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = MineAvarice3
    {
        GoSub CreateStatusMenu
        GoSub MineAvarice3
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = RecallAvarice3
    {
        GoSub CreateStatusMenu
        GoSub Recall 7
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = MineAvarice4
    {
        GoSub CreateStatusMenu
        GoSub MineAvarice4
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = RecallAvarice4
    {
        GoSub CreateStatusMenu
        GoSub Recall 8
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }
    If #menuButton = Insanity
    {
        GoSub CreateStatusMenu
        GoSub Insanity
        Set #menuButton Nothing
        Menu Clear
        GoSub CreateMainMenu
    }

; /////////////////////
; //  Runebook Menu  //
; /////////////////////

    If RB in #MenuButton
    {
        If #MenuButton = RB1
        {
            GoSub CreateStatusMenu
            GoSub Recall 1
        }
        If #MenuButton = RB2
        {
            GoSub CreateStatusMenu
            GoSub Recall 2
        }
        If #MenuButton = RB3
        {
            GoSub CreateStatusMenu
            GoSub Recall 3
        }
        If #MenuButton = RB4
        {
            GoSub CreateStatusMenu
            GoSub Recall 4
        }
        If #MenuButton = RB5
        {
            GoSub CreateStatusMenu
            GoSub Recall 5
        }
        If #MenuButton = RB6
        {
            GoSub CreateStatusMenu
            GoSub Recall 6
        }
        If #MenuButton = RB7
        {
            GoSub CreateStatusMenu
            GoSub Recall 7
        }
        If #MenuButton = RB8
        {
            GoSub CreateStatusMenu
            GoSub Recall 8
        }

        Set #menuButton Nothing
        GoSub CreateMainMenu
    }

    If #menuButton = Quit
    {
        menu Hide
        Halt
    }
    
    Goto Mainloop

; ////////////////////////////////////////////////////////////////
; //  Initialize Global Variables, Open Containers, Move Tools  //
; ////////////////////////////////////////////////////////////////

Sub Initialize
    Menu Clear
    Menu Window Size 320 50
    Menu Window Title [YO] Blacksmith Suite v1.0
    Menu Text Status1 10 5
    Menu Text Status2 10 25
    Menu Show

    Menu Set Status1 Loading [YO] Mining Suite
    Menu Set Status2 Initializing Global Variables
    Set %ShortDelay 5 ; Quarter Second Delay
    Set %MedDelay 10 ; Half Second Delay
    Set %LongDelay 15 ; Three-Quarters Second Delay
    Set %MiningTool QPF_TWF ; Pickaxe / Shovel Object Types
    Set %MiningToolCount 0 ; Init
    Set %ToolBag UYOYKMD ; Mining Tool Container ID (Container for Ingots and Tools)
    Set %RuneBag KHYZLMD ; Bag Containing Runebook
    Set %BankIngotContainer FDWGMMD

    Set %UseProspector #True
    Set %ProspectorTool GBG
    
    Set %Ore TVJ_DWJ_EWJ_GWJ ; Ore Object Types
    Set %Ingots ENK ; Ingot Object Type

    ; ////////////////////////
    ; //  Ore Color Values  //
    ; ////////////////////////
    Menu Set Status2 Initializing Ore Variables
    Set %IronCol 0
    Set %IronLabel Iron
    Set %DullCopperCol 2419
    Set %DullCopperLabel Dull Copper
    Set %ShadowCol 2406
    Set %ShadowLabel Shadow
    Set %CopperCol 2413
    Set %CopperLabel Copper
    Set %BronzeCol 2418
    Set %BronzeLabel Bronze
    Set %GoldCol 2213
    Set %GoldLabel Gold
    Set %AgapiteCol 2425
    Set %AgapiteLabel Agapite
    Set %VeriteCol 2207
    Set %VeriteLabel Verite
    Set %ValoriteCol 2219
    Set %ValoriteLabel Valorite

    Set %PackHorse ZGFMMMD

    Menu Set Status2 Opening Backpack
    Set #LObjectID #BackpackID
    Event Macro 17 0 ; Open Main Backpack
    Wait %MedDelay
    ContPos 0 760
    Wait %ShortDelay
    
    Menu Set Status2 Opening Mining Container
    Set #LObjectID %ToolBag
    Event Macro 17 0
    Wait %MedDelay
    ContPos 220 775
    Wait %ShortDelay

    Menu Set Status2 Opening Runebook Container
    Set #LObjectID %RuneBag
    Event Macro 17 0 ; Open Mining Tool Bag
    Wait %MedDelay
    ContPos 395 610
    Wait %ShortDelay
    
    GoSub ToolInit
    Return

Sub ToolInit
    Menu Set Status1 Initializing Mining Tools
    Menu Set Status2 Checking Mining Tools
    GoSub ToolCheck
    FindItem %MiningTool C_ , #BackpackID ; Find Mining Tools in Main Backpack
    While #FindKind <> -1
    {
        Set %ToolsRemaining #FindCnt
        Menu Set Status2 Organizing Mining Tools: %ToolsRemaining Remaining
        ExEvent Drag #FindID 1 ; Drag Mining Tool
        Wait %MedDelay
        ExEvent DropC %ToolBag 400 0; Drop Mining Tool in Mining Tool Bag
        Wait %MedDelay
        FindItem %MiningTool C_ , #BackpackID
    }
    
    FindItem %ProspectorTool C_ , #BackpackID ; Find Mining Tools in Main Backpack
    While #FindKind <> -1
    {
        Set %ToolsRemaining #FindCnt
        Menu Set Status2 Organizing Prospector Tools: %ToolsRemaining Remaining
        ExEvent Drag #FindID 1 ; Drag Mining Tool
        Wait %MedDelay
        ExEvent DropC %ToolBag 400 0; Drop Mining Tool in Mining Tool Bag
        Wait %MedDelay
        FindItem %MiningTool C_ , #BackpackID
    }
    Return

Sub CreateStatusMenu
    Menu Clear
    Menu Window Size 320 95
    Menu Window Title [YO] Blacksmith Suite v1.0
    Menu Text Status1 10 5
    Menu Text Status2 10 25

    Menu Text MiningTools 10 50 Mining Tools: %MiningToolCount Remaining
    Menu Text ProspectorTools 10 70 Prospector Tools: %ProspectorToolCount Remaining
    Menu Show
    Return

Sub CreateMainMenu
    ; Column X-Coordinates
    Set %MenuCol 15

    Set %2_Col1 10
    Set %2_Col2 180

    Set %3_Col1 10
    Set %3_Col2 125
    Set %3_Col3 240

    Set %4_Col1 10
    Set %4_Col2 95
    Set %4_Col3 180
    Set %4_Col4 265

    Set %AMR_Col 150

    Set %OptionsCol 10

    Set %RB_Col1 285
    Set %RB_Col2 %RB_Col1 + 35

    ; Row Y-Coordinates
    Set %TopRow 15

    Set %MiningMenuRow1 45
    Set %MiningMenuRow2 %MiningMenuRow1 + 20
    Set %MiningMenuRow3 %MiningMenuRow2 + 25

    Set %PackAnimalMenuRow1 %MiningMenuRow3 + 40
    Set %PackAnimalMenuRow2 %PackAnimalMenuRow1 + 20

    Set %AutoMineMenuRow1 %PackAnimalMenuRow2 + 40
    Set %AutoMineMenuRow2 %AutoMineMenuRow1 + 20
    Set %AutoMineMenuRow3 %AutoMineMenuRow2 + 40
    Set %AutoMineMenuRow4 %AutoMineMenuRow3 + 40
    Set %AutoMineMenuRow5 %AutoMineMenuRow4 + 40
    Set %AutoMineMenuRow6 %AutoMineMenuRow5 + 40
    Set %AutoMineMenuRow7 %AutoMineMenuRow6 + 45

    Set %RunebookRow1 %AutoMineMenuRow1
    Set %RunebookRow2 %RunebookRow1 + 20
    Set %RunebookRow3 %RunebookRow2 + 25
    Set %RunebookRow4 %RunebookRow3 + 25
    Set %RunebookRow5 %RunebookRow4 + 25
    Set %RunebookRow6 %RunebookRow5 + 25
    Set %RunebookRow7 %RunebookRow6 + 25
    Set %RunebookRow8 %RunebookRow7 + 25
    Set %RunebookRow9 %RunebookRow8 + 25
    Set %RunebookRow10 %RunebookRow9 + 25

    Set %OptionsMenu  %AutoMineMenuRow7 + 55

    Set %QuitMenu %AutoMineMenuRow7 + 55

    ; Button Dimensons
    Set %2_ButtonX 165
    Set %3_ButtonX 110
    Set %4_ButtonX 85
    Set %AM_ButtonX 140
    Set %AMR_ButtonX 50
    Set %RB_ButtonX 30

    Set %ButtonY 25
    Set %ButtonY_Slim 20
    Set %ButtonY_Large 40

    Menu Clear
    Menu Window Size 360 516
    Menu Window Title [YO] Mining Suite v1.4

    ; Top Row
    Menu Text ProspectorTools 10 %TopRow Prospector Tools: %MiningToolCount
    Menu Text MiningTools 150 %TopRow Mining Tools: %MiningToolCount
    Menu Text Coords 290 %TopRow

    ; Mining Menu
    Menu Text MineMenu %MenuCol %MiningMenuRow1 Mining Menu

    ; Mining Menu Row 1
    Menu Button Mine %4_Col1 %MiningMenuRow2 %4_ButtonX %ButtonY Mine
    Menu Button Reinitialize %4_Col2 %MiningMenuRow2 %4_ButtonX %ButtonY Reinitialize
    Menu Button SelfIngots %4_Col3 %MiningMenuRow2 %4_ButtonX %ButtonY Backpack
    Menu Button BankIngots %4_Col4 %MiningMenuRow2 %4_ButtonX %ButtonY Bank

    ; Mining Menu Row 2
    Menu Button Smelt %4_Col1 %MiningMenuRow3 %4_ButtonX %ButtonY Smelt
    Menu Button Consolidate %4_Col2 %MiningMenuRow3 %4_ButtonX %ButtonY Consolidate
    Menu Button DumpOre %4_Col3 %MiningMenuRow3 %4_ButtonX %ButtonY Dump
    Menu Button RareOre %4_Col4 %MiningMenuRow3 %4_ButtonX %ButtonY P/U Rare

    ; Pack Animal Menu
    Menu Text AnimalMenu %MenuCol %PackAnimalMenuRow1 Pack Animal Menu
    Menu Button LoadHorse %4_Col1 %PackAnimalMenuRow2 %4_ButtonX %ButtonY Load Horse
    Menu Button Come %4_Col2 %PackAnimalMenuRow2 %4_ButtonX %ButtonY Come
    Menu Button Follow %4_Col3 %PackAnimalMenuRow2 %4_ButtonX %ButtonY Follow
    Menu Button Stay %4_Col4 %PackAnimalMenuRow2 %4_ButtonX %ButtonY Stay

    ; Auto-Mine Menu
    Menu Text AutoMineMenu %MenuCol %AutoMineMenuRow1 Auto-Mine Menu
    Menu Button MineMinoc %3_Col1 %AutoMineMenuRow2 %AM_ButtonX %ButtonY_Large Minoc East Cave
    Menu Button RecallMinoc %AMR_Col %AutoMineMenuRow2 %AMR_ButtonX %ButtonY_Large Recall

    Menu Button MineAvarice1 %3_Col1 %AutoMineMenuRow3 %AM_ButtonX %ButtonY_Large Avarice Cave 1
    Menu Button RecallAvarice1 %AMR_Col %AutoMineMenuRow3 %AMR_ButtonX %ButtonY_Large Recall

    Menu Button MineAvarice2 %3_Col1 %AutoMineMenuRow4 %AM_ButtonX %ButtonY_Large Avarice Cave 2
    Menu Button RecallAvarice2 %AMR_Col %AutoMineMenuRow4 %AMR_ButtonX %ButtonY_Large Recall

    Menu Button MineAvarice3 %3_Col1 %AutoMineMenuRow5 %AM_ButtonX %ButtonY_Large Avarice Cave 3
    Menu Button RecallAvarice3 %AMR_Col %AutoMineMenuRow5 %AMR_ButtonX %ButtonY_Large Recall

    Menu Button MineAvarice4 %3_Col1 %AutoMineMenuRow6 %AM_ButtonX %ButtonY_Large Avarice Cave 4
    Menu Button RecallAvarice4 %AMR_Col %AutoMineMenuRow6 %AMR_ButtonX %ButtonY_Large Recall

    ; Runebook Menu
    Menu Text RunebookMenu 290 %RunebookRow1 Runebook
    Menu Button RB1 %RB_Col1 %RunebookRow2 %RB_ButtonX %ButtonY 1
    Menu Button RB2 %RB_Col1 %RunebookRow3 %RB_ButtonX %ButtonY 2
    Menu Button RB3 %RB_Col1 %RunebookRow4 %RB_ButtonX %ButtonY 3
    Menu Button RB4 %RB_Col1 %RunebookRow5 %RB_ButtonX %ButtonY 4
    Menu Button RB5 %RB_Col1 %RunebookRow6 %RB_ButtonX %ButtonY 5
    Menu Button RB6 %RB_Col1 %RunebookRow7 %RB_ButtonX %ButtonY 6
    Menu Button RB7 %RB_Col1 %RunebookRow8 %RB_ButtonX %ButtonY 7
    Menu Button RB8 %RB_Col1 %RunebookRow9 %RB_ButtonX %ButtonY 8
    Menu Button RB9 %RB_Col2 %RunebookRow2 %RB_ButtonX %ButtonY 9
    Menu Button RB10 %RB_Col2 %RunebookRow3 %RB_ButtonX %ButtonY 10
    Menu Button RB11 %RB_Col2 %RunebookRow4 %RB_ButtonX %ButtonY 11
    Menu Button RB12 %RB_Col2 %RunebookRow5 %RB_ButtonX %ButtonY 12
    Menu Button RB13 %RB_Col2 %RunebookRow6 %RB_ButtonX %ButtonY 13
    Menu Button RB14 %RB_Col2 %RunebookRow7 %RB_ButtonX %ButtonY 14
    Menu Button RB15 %RB_Col2 %RunebookRow8 %RB_ButtonX %ButtonY 15
    Menu Button RB16 %RB_Col2 %RunebookRow9 %RB_ButtonX %ButtonY 16

    Menu Font BGColor Red
    Menu Button Quit %3_Col3 %QuitMenu %3_ButtonX 36 Quit

    Menu Font BGColor White
    Menu List Create Options %OptionsCol %OptionsMenu 150 36
    Menu List Add Options Enable Pack Horse
    Menu List Add Options Enable Portable Forge
    Menu List Select Options 2

    Menu Font BGColor Black
    Menu Font Color White
    Menu Button Insanity %3_Col1 %AutoMineMenuRow7 340 50 THIS IS INSANITY!!!

    Menu Show
    Return

Sub ToolCheck ; Mining tool interrupt
    FindItem %MiningTool C ; Find Mining Tool in any Open Containers
    Wait 5
    Set %MiningToolCount #FindCnt
    Set %MiningToolID #FindID
    If #FindCnt = 0
    {
        Display You have run out of mining tools.
        GoSub CreateMainMenu
        Goto Mainloop
    }
    Menu Set MiningTools Mining Tools: %MiningToolCount
    If %UseProspector = #True
    {
        FindItem %ProspectorTool C
        If #FindCnt = 0
            Set %ProspectorToolCount 0
        Else
        {
            Set %ProspectorToolCount #FindCnt
            Set %ProspectorToolID #FindID
        }
    }
    Menu Set ProspectorTools Prospector Tools: %ProspectorToolCount
    Return

Sub MineMinoc
    GoSub Pathfind 2560 496 ; Starting Position / Node 1
    GoSub MineCircle
    GoSub Pathfind 2560 488 ; Node 2
    GoSub MineCircle
    GoSub Pathfind 2568 488 ; Node 3
    GoSub MineCircle
    GoSub Pathfind 2568 480 ; Node 4
    GoSub MineCircle
    GoSub Pathfind 2576 480 ; Node 5
    GoSub MineCircle
    GoSub MoveIngots #BackpackID %ToolBag
    GoSub OrganizeIngots %ToolBag
    Return

Sub MineAvarice1
    GoSub Pathfind 2448 896 ; Starting Position / Node 1
    GoSub MineCircle
    GoSub Pathfind 2440 896 ; Position / Node 2
    GoSub MineCircle
    GoSub Pathfind 2440 904 ; Position / Node 3
    GoSub MineCircle
    GoSub Pathfind 2432 904 ; Position / Node 4
    GoSub MineCircle
    GoSub Pathfind 2440 904 ; Run to Position 4
    GoSub Pathfind 2448 904 ; Position / Node 5
    GoSub MineCircle
    GoSub Pathfind 2448 896 ; Run to Position 1
    GoSub Pathfind 2448 888 ; Position / Node 6
    GoSub MineCircle
    GoSub Pathfind 2448 880 ; Position / Node 7
    GoSub MineCircle
    GoSub Pathfind 2439 880 ; Run to Position 8
    GoSub MineCircle
    GoSub Pathfind 2432 880 ; Position / Node 9
    GoSub MineCircle
    GoSub Pathfind 2433 872 ; Position / Node 10
    GoSub MineCircle
    GoSub Pathfind 2440 871 ; Run to Position 11
    GoSub MineCircle
    GoSub MoveIngots #BackpackID %ToolBag
    GoSub OrganizeIngots %ToolBag
    Return
    
Sub MineAvarice2
    GoSub Pathfind 2384 912 ; Starting Position / Node 1
    GoSub MineCircle
    GoSub Pathfind 2384 904 ; Position / Node 2
    GoSub MineCircle
    GoSub Pathfind 2376 904 ; Position / Node 3
    GoSub MineCircle
    GoSub Pathfind 2384 904 ; Run to Position 2
    GoSub Pathfind 2392 904 ; Position / Node 4
    GoSub MineCircle
    GoSub Pathfind 2400 912 ; Position / Node 5
    GoSub MineCircle
    GoSub MoveIngots #BackpackID %ToolBag
    GoSub OrganizeIngots %ToolBag
    Return

Sub MineAvarice3
    GoSub Pathfind 2336 824 ; Starting Position / Node 1
    GoSub MineCircle
    GoSub Pathfind 2328 816 ; Node 2
    GoSub MineCircle
    GoSub Pathfind 2320 816 ; Node 3
    GoSub MineCircle
    GoSub Pathfind 2320 824 ; Node 4
    GoSub MineCircle
    GoSub MoveIngots #BackpackID %ToolBag
    GoSub OrganizeIngots %ToolBag
    Return

Sub MineAvarice4
    GoSub Pathfind 2352 808 ; Starting Position / Node 1
    GoSub MineCircle
    GoSub Pathfind 2352 800 ; Node 2
    GoSub MineCircle
    GoSub Pathfind 2360 808 ; Node 3
    GoSub MineCircle
    GoSub Pathfind 2360 816 ; Node 4
    GoSub MineCircle
    GoSub Pathfind 2368 824 ; Node 5
    GoSub MineCircle
    GoSub MoveIngots #BackpackID %ToolBag
    GoSub OrganizeIngots %ToolBag
    Return

Sub MineCircle ; Mine Every Surrounding Tile
    GoSub ToolCheck
    Menu Font BGColor Red
    Menu Button Stop 270 5 45 30 Stop
    For %y -1 1 ; y-axis
    {
        If %y = -1
            Set %yLabel N
        If %y = 1
            Set %yLabel S
        For %x -1 1 ; x-axis
        {
            If %x = -1
                Set %xLabel W
            If %x = 1
                Set %xLabel E
            If %x <> 0
            {
                If %y <> 0
                {
                    Set %Node %yLabel , %xLabel
                    Set #LTargetKind 2
                    Set #LTargetX #CharPosX + %x
                    Set #LTargetY #CharPosY + %y

                    If %UseProspector = #True
                    {
                        Menu Set Status2 Prospecting %Node Node
                        Set #LObjectID %ProspectorToolID
                        Wait %ShortDelay
                        Event Macro 17 0 ; LastObject
                        Target
                        Event Macro 22 0 ; LastTarget
                        Wait %MedDelay
                    }
                    Set %MineTrigger #True
                    While %MineTrigger <> #False
                    {
                        If Stop in #MenuButton
                        {
                            GoSub CreateMainMenu
                            Goto Mainloop
                        }
                        Menu Set Status2 Mining %Node Node
                        gosub ToolCheck
                        Set #LObjectID %MiningToolID
                        Wait %ShortDelay
                        Event Macro 17 0 ; LastObject
                        Target
                        Event Macro 22 0 ; LastTarget
                        Wait %ShortDelay
                        If There_is_no_metal_here in #sysMsg || You_can't_mine_that in #sysMsg || That_is_too_far_away in #sysMsg || You_can't_mine_there in #sysMsg
                            Set %MineTrigger #False
                    }
                }
            }
        }
    }
    GoSub WeightCheck
    Return

Sub WeightCheck ; Check if Maximum Weight
    Menu Set Status2 Checking Weight
    If #Weight > #MaxWeight
        GoSub Smelt
    Return

Sub DumpPack ; Dump All Ore From Open Packs
    Menu Set Status2 Dumping Ore From Backpack
    If %UsePackHorse = #True
        Event Macro 1 0 All Come
    FindItem %Ore C
    Wait %ShortDelay
    For #FindIndex 1 #FindCnt
    {
        ExEvent Drag #FindID #FindStack
        Wait %MedDelay
        ExEvent DropG #CharPosX #CharPosY #CharPosZ
        Wait %MedDelay
    }
    Return

Sub DumpHorse
    Menu Set Status2 Dumping Ore From Pack Horse
    Set #LObjectID %PackHorse
    Event Macro 17 0
    Wait %MedDelay
    FindItem %Ore C_ , %PackHorse
    Wait %ShortDelay
    For #FindIndex 1 #FindCnt
    {
        ExEvent Drag #FindID #FindStack
        Wait %MedDelay
        ExEvent DropG #CharPosX #CharPosY #CharPosZ
        Wait %MedDelay
    }
    Return

Sub ConsolidateOre ; Comsolidate Ore Stacks on Ground by Type
    Menu Set Status2 Consolidating %2 Ore
    FindItem %Ore G_2
    %ShortDelay
    For #FindIndex 1 #FindCnt
    {
        If #FindCol = %1
        {
            Set #LTargetID #FindID
            Set #LTargetKind 1
        }
    }
    For #FindIndex 1 #FindCnt
    {
        If #FindCol = %1
        {
            If #FindID <> #LTargetID
            {
                Set #LObjectID #FindID
                Event Macro 17 0
                Target
                Event Macro 22 0
                Wait %LongDelay
            }
        }
    }
    Return

Sub LoadHorse ; Load Ore Stacks on Ground Into Pack Horse
    Menu Set Status2 Loading Ore Onto Pack Horse
    If %UsePackHorse = #True
        Event Macro 1 0 All Come
    FindItem %Ore G_2
    Wait %ShortDelay
    For #FindIndex 0 #FindCnt
    {
        ExEvent Drag #FindID #FindStack
        Wait %MedDelay
        ExEvent DropC %PackHorse
        Wait %MedDelay
    }
    Return

Sub PickupRareOre ; Load Ore Stacks on Ground Into Pack Horse
    Menu Set Status2 Picking Up Rare Ore
    FindItem %Ore G_2
    Wait %ShortDelay
    For #FindIndex 0 #FindCnt
    {
        If #FindCol = %BronzeCol 2418 || #FindCol = %GoldCol 2213 || #FindCol = %AgapiteCol 2425 || #FindCol = %VeriteCol 2207 || #FindCol = %ValoriteLabel
        {
            ExEvent Drag #FindID #FindStack
            Wait %MedDelay
            ExEvent DropC #BackpackID
            Wait %MedDelay
    }
    Return

Sub Smelt
    Set %Forge JBG ; Forge Object Type
    If %UsePackHorse = #True
    {
        GoSub LoadHorse
        Return
    }
    If %UsePortForge = #True
        FindItem %Forge C
    Else
        FindItem %Forge G_2
    If #FindCnt >= 1
    {
        GoSub DumpPack %1
        GoSub ConsolidateOre %IronCol %IronLabel
        GoSub ConsolidateOre %DullCopperCol %DullCopperLabel
        GoSub ConsolidateOre %ShadowCol %ShadowLabel
        GoSub ConsolidateOre %CopperCol %CopperLabel
        GoSub ConsolidateOre %BronzeCol %BronzeLabel
        GoSub ConsolidateOre %GoldCol %GoldLabel
        GoSub ConsolidateOre %AgapiteCol %AgapiteLabel
        GoSub ConsolidateOre %VeriteCol %VeriteLabel
        GoSub ConsolidateOre %ValoriteCol %ValoriteLabel
        FindItem %Forge C
        Set #LTargetID #FindID
        Set #LTargetKind 1

        Set %SmeltTrigger #True
        While %SmeltTrigger = #True
        {
            Menu Set Status2 Smelting Ore
            FindItem %Ore G_2
            Wait %ShortDelay
            For #FindIndex 1 #FindCnt
            {
                If #FindCnt <> 0
                {
                    If #FindStack <> 1
                    {
                        Set #LObjectID #FindID
                        Event Macro 17 0
                        Target
                        Event Macro 22 0
                        Wait %LongDelay
                    }
                    Else
                    {
                        ExEvent Drag #FindID #FindStack
                        Wait %MedDelay
                        ExEvent DropC %ToolBag 0 166
                        Wait %MedDelay
                    }
                }
                Else
                {
                    Set %SmeltTrigger #False
                }
            }
        }
    }
    Else
        Display There are no forges near you.
    Return

Sub MoveIngots
    Menu Set Status2 Moving Ingots
    FindItem %Ingots C_ , %1
    Wait %LongDelay
    For #FindIndex 1 #FindCnt
    {
        ExEvent Drag #FindID #FindStack
        Wait %MedDelay
        ExEvent DropC %2
        Wait %MedDelay
    }
    Return

Sub OrganizeIngots
    Set %Row1 0
    Set %Row2 75

    Set %Col1 0
    Set %Col2 40
    Set %Col3 60
    Set %Col4 80
    Set %Col5 100

    Menu Set Status2 Organizing Ingots
    FindItem %Ingots C_ , %1
    Wait %LongDelay
    For #FindIndex 1 #FindCnt
    {
        If #FindCol = %IronCol
        {
            Menu Set Status2 Organizing Iron Ingots
            Set %IngotX %Col1
            Set %IngotY %Row1
        }
        If #FindCol = %DullCopperCol
        {
            Menu Set Status2 Organizing Dull Copper Ingots
            Set %IngotX %Col2
            Set %IngotY %Row1
        }
        If #FindCol = %ShadowCol
        {
            Menu Set Status2 Organizing Shadow Ingots
            Set %IngotX %Col3
            Set %IngotY %Row1
        }
        If #FindCol = %CopperCol
        {
            Menu Set Status2 Organizing Copper Ingots
            Set %IngotX %Col4
            Set %IngotY %Row1
        }
        If #FindCol = %BronzeCol
        {
            Menu Set Status2 Organizing Bronze Ingots
            Set %IngotX %Col1
            Set %IngotY %Row2
        }
        If #FindCol = %GoldCol
        {
            Menu Set Status2 Organizing Gold Ingots
            Set %IngotX %Col2
            Set %IngotY %Row2
        }
        If #FindCol = %AgapiteCol
        {
            Menu Set Status2 Organizing Agapite Ingots
            Set %IngotX %Col3
            Set %IngotY %Row2
        }
        If #FindCol = %VeriteCol
        {
            Menu Set Status2 Organizing Verite Ingots
            Set %IngotX %Col4
            Set %IngotY %Row2
        }
        If #FindCol = %ValoriteCol
        {
            Menu Set Status2 Organizing Valorite Ingots
            Set %IngotX %Col5
            Set %IngotY %Row2
        }

        ExEvent Drag #FindID #FindStack
        Wait %MedDelay
        ExEvent DropC %2 %IngotX %IngotY
        Wait %MedDelay
    }
    Return

Sub 8x8
    Set %x #CharPosX % 8
    Set %y #CharPosY % 8
    Menu Set Coords X: %x / Y: %y
    Return

Sub Pathfind
    Menu Set Status2 Pathfinding to X: %1 Y: %2
    Event PathFind %1 %2
    Set %PathfindTrigger #True
    While %PathfindTrigger #True
    {
       If #CharPosX = %1 && #CharPosY = %2
           Set %PathfindTrigger #False
    }
    Return
    
Sub Recall
    Set %RecallX 234

    If %1 = 1
    {
        Set %Location Yew , #spc , Library
        Set %RecallY 170
    }
    If %1 = 2
    {
        Set %Location West , #spc , Brit , #spc , Bank
        Set %RecallY 185
    }
    If %1 = 3
    {
        Set %Location Minoc , #spc , Bank
        Set %RecallY 200
    }
    If %1 = 4
    {
        Set %Location Minoc , #spc , East , #spc , Mine
        Set %RecallY 215
    }
    If %1 = 5
    {
        Set %Location Avarice , #spc , Cav , #spc , One
        Set %RecallY 230
    }
    If %1 = 6
    {
        Set %Location Avarice , #spc , Cave , #spc , Two
        Set %RecallY 245
    }
    If %1 = 7
    {
        Set %Location Avarice , #spc , Cave , #spc , Three
        Set %RecallY 260
    }
    If %1 = 8
    {
        Set %Location Avarice , #spc , Cave , #spc , Four
        Set %RecallY 275
    }
    Menu Set Status2 Recalling to %Location

    Set %RuneBook MPF
    FindItem %RuneBook C_ , %RuneBag
    Set #LObjectID #FindID
    Set %OldX #CharPosX
    Set %OldY #CharPosY

    Set %RecallTrigger #True
    While %RecallTrigger = #True
    {
        Event Macro 17 0
        Wait %ShortDelay
        ContPos 100 100
        Wait %ShortDelay
        Click %RecallX %RecallY
        Wait 2s
        If %OldX <> #CharPosX && %OldY <> #CharPosY
            Set %RecallTrigger #False
    }
    Return
    
Sub Insanity
    Menu Set Status1 Insanity Routine: Mining Minoc East Cave
    GoSub Recall 4
    GoSub MineMinoc

    Menu Set Status1 Insanity Routine: Mining Avarice Cave 1
    GoSub Recall 5
    GoSub MineAvarice1

    Menu Set Status1 Insanity Routine: Mining Avarice Cave 2
    GoSub Recall 6
    GoSub MineAvarice2

    Menu Set Status1 Insanity Routine: Mining Avarice Cave 3
    GoSub Recall 7
    GoSub MineAvarice3

    Menu Set Status1 Insanity Routine: Mining Avarice Cave 4
    GoSub Recall 8
    GoSub MineAvarice4

    Menu Set Status1 Insanity Routine: Post-Cleanup
    GoSub Recall 3
    Wait 2s
    Gosub MoveIngots %ToolBag %BankIngotContainer
    GoSub OrganizeIngots %BankIngotContainer
    Return
